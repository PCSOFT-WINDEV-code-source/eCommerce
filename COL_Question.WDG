#To edit and compare internal_properties, use WINDEV integrated tools.
#Internal properties refer to the properties of controls in windows, reports, etc.
info :
 name : COL_Question
 major_version : 28
 minor_version : 0
 type : 7
 description : ""
 subtype : 0
procedure_set :
 identifier : 0x1ebac7801744feb3
 internal_properties : CAAAAAgAAABGLu41kG7fjQV3iS4F72qmnKaNh5694reolNKIW0iw
 code_elements :
  type_code : 31
  p_codes :
   -
     internal_properties : CAAAAAgAAACko1DOYSGWiEfmi47+g0xrryBFucjRJOZNM28b4cO+S1M4MC2h4adn0pITUUH+X+gT4JsjJwu3rpWi0Nt9BiXJMn3gzThsJTpDY5C2E6bDK8jnmlOjc+813c/t3AK14IdkK2kd7CvOV5R9lQ==
     code : |1-
      // Cette structure correspond à la structure décrite dans l'analyse pour le fichier Question
      // Cette structure est utilisée pour la gestion du backoffice et pour le Web Service
      STCOL_Question est une structure
      		IDQuestion est un entier
      		Libellé est un chaîne
      		
      Fin
       
       // Tableau des questions définies
       gtabQuestions est un tableau de STCOL_Question
     type : 720896
  procedures :
   -
     name : AjouterQuestion
     internal_properties : CAAAAAgAAAAeQeZWqR4YD9gNuw5ISpse4q5d4YfQh8vxSSLWonylqfGpzXAstIQfjtu5yNZJ9N/c91UXiZv53fcJCNn0zKQH2jXtD1BPimLproJX87E+1RbRiyhFUUvyPW0p6P5mipLcRRthNLb+/G4cHMU2fOcWaLmnLrlIMic+6HTcyeP/rQgO
     procedure_id : 2245711568744549534
     type_code : 15
     code : |1-
      // Résumé : Ajoute une question
      // Syntaxe :
      //[ <Résultat> = ] AjouterQuestion (<sTexteQuestion> est chaîne)
      //
      // Paramètres :
      //	sTexteQuestion (chaîne ANSI) : Texte de la question
      // Valeur de retour :
      // 	entier : Identifiant de la question, -1 en cas d'erreur
      //
      //// Traitement automatique des exceptions :   exécuter le bloc de code CAS EXCEPTION:
      //
      
      Fonction AjouterQuestion( sTexteQuestion est un chaîne)
      
      // Variables locales
      nIDQuestion		est un entier	= -1
      
      // Initialisation du mécanisme de remontée des erreurs
      InitialiseErreur()
      
      // Vérification de l'identification
      SI bEstConnectéWSBackOffice() = Faux ALORS
      	AjouteErreur(CST_MESSAGE_ERREUR_DROITS)
      	RENVOYER nIDQuestion
      FIN
      
      // On note la modification de configuration
      NotifierModificationConfiguration("Ajout d'une question","",sTexteQuestion)
      
      // Initialisation de l'enregistrement
      HRaz(Question)
      
      // On remplit les données
      Question.Libellé	= sTexteQuestion
      
      // Ajout en base
      SI HAjoute(Question) = Faux ALORS
      	AjouteErreur("Ajout de l'enregistrement impossible")
      	RENVOYER nIDQuestion
      FIN
      
      // On mémorise la question créée
      nIDQuestion = Question.IDQuestion
      
      // Mise à jour 
      ActualiserQuestions()
      
      // On renvoie l'identifiant de la question
      RENVOYER nIDQuestion
      
      CAS EXCEPTION:
      	AjouteErreur("Exception détectée",ExceptionInfo())
      	RENVOYER nIDQuestion
     type : 458752
   -
     name : ListeQuestions
     procedure_id : 2245711568744615226
     type_code : 15
     code : |1-
      // Résumé : Renvoie la liste des questions proposées
      // Syntaxe :
      //[ <Résultat> = ] ListeQuestions ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	tableau : tableau de STCOL_Question
      //
      
      Fonction ListeQuestions( )
      
      // Initialisation du mécanisme de remontée des erreurs
      InitialiseErreur()
      
      // Vérification de l'identification
      SI bEstConnectéWSBackOffice() = Faux ALORS
      	AjouteErreur(CST_MESSAGE_ERREUR_DROITS)
      	RENVOYER gtabQuestions
      FIN
      
      // Est-ce que les questions ont été initialisées ?
      Si gtabQuestions..Occurrence = 0 alors
      	// On initialise le contenu
      	ActualiserQuestions()
      FIN
      
      // On renvoie la liste des questions
      renvoyer gtabQuestions
     type : 458752
   -
     name : ActualiserQuestions
     procedure_id : 2245712359018781818
     type_code : 15
     code : |1+
      // Résumé : Actualise la liste des questions
      // Syntaxe :
      //ActualiserQuestions ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	Aucune
      //
      
      Procédure ActualiserQuestions( )
      
      // Initialisation du mécanisme de remontée des erreurs
      InitialiseErreur()
      
      // Vérification de l'identification
      SI bEstConnectéWSBackOffice() = Faux ALORS
      	AjouteErreur(CST_MESSAGE_ERREUR_DROITS)
      	RETOUR
      FIN
      
      // On remet à jour le contenu
      FichierVersTableau(gtabQuestions,Question)
     type : 458752
  procedure_templates : []
  property_templates : []
 code_parameters :
  internal_properties : CAAAAAgAAACVGQgYbalU7DKC3oH4ItDrjtcTHXXV/EuH8q0IbhyQlw==
resources :
 string_res :
  identifier : 0x10225f07108d8915
  internal_properties : CAAAAAgAAACm76HWfKGWp33VjXInA4cRlqArlgTTA862QGt72W2ld5Y=
custom_note :
 internal_properties : CAAAAAgAAABtB9HWVzrXO2+4NDRVK0vmzaNKrCKqH1DBX30lMmGZ
