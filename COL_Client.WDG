#To edit and compare internal_properties, use WINDEV integrated tools.
#Internal properties refer to the properties of controls in windows, reports, etc.
info :
 name : COL_Client
 major_version : 28
 minor_version : 0
 type : 7
 description : ""
 subtype : 0
procedure_set :
 identifier : 0x1eb15acf054fddf6
 internal_properties : CAAAAAgAAABGLu41kG7fjQV3iS4F72qmnKaNh5694reolNKIW0iw
 code_elements :
  type_code : 31
  p_codes :
   -
     internal_properties : CAAAAAgAAABd9EuFPvcyCBrrXOQPLOBcDS8+p1H0fs6jAZXZAJLWpNgNsHR5uCN5sK/u4JuiLu8gV+QtM+NRI3kk9wy4Cw+ruldoZjdLQxdM9I+HcNpqCQzwYoZiLX+EmtRgTWoOKqSiDcdv+9edUnuM+AiXqeZi0ruI7Ii9m1/D37q1JpDq
     code : |1-
      // Cette structure correspond à la structure décrite dans l'analyse pour le fichier Client
      // Cette structure est utilisée pour la gestion du backoffice et pour le Web Service
      STCOL_Client est une structure
      		IDClient 			est un entier //Identifiant de Client
      		Civilité 			est un entier sans signe sur 1 octet //Civilité
      		Nom 				est un chaîne//Nom
      		Prénom 				est un chaîne//Prénom
      		Email 				est un chaîne//Email
      		Téléphone 			est un chaîne//Téléphone
      		Réponse 			est un chaîne//Réponse
      		DateCréation 		est un date //Date de création
      		IDQuestion 			est un entier //IDQuestion
      		NomAffichage		est une chaine
      Fin
       
       PUBLIC
       	gtabClients est un tableau de STCOL_Client
       
     type : 720896
  procedures :
   -
     name : ListeClients
     internal_properties : CAAAAAgAAAAqhXbAQ85bubT0Czlm6MNqZZJeQibFMRa38jzNbVbSOpYNW7o5qNSpDkZCkDak4couHpZ7TlTRY36qMbY8Orz91WYwdLCsK17UjJ4FDTpxGr88rGoeQwgX5YrldbA95YA8m7wgBcZK9Qn0LwABGTRd8kBG46300PsWw7J8O/6Ov5tlHV8sT74rwa5wuJ35rAW3hJdWycI=
     procedure_id : 2219417426914382132
     type_code : 15
     code : |1-
      // Résumé : Liste les clients
      // Syntaxe :
      //[ <Résultat> = ] ListeClients (<nIDClient> est entier, <sNomClient> est chaîne, <sEmailClient> est chaîne)
      //
      // Paramètres :
      //	nIDClient (entier) : Identifiant du client (-1 pour ignorer)
      //	sNomClient (chaîne) : Nom du client à rechercher (chaine vide pour ignorer)
      //	sEmailClient (chaîne) : Email du client à rechercher (chaine vide pour ignorer)
      // Valeur de retour :
      // 	tableau : Tableau de STCOL_Client
      //
      //// Traitement automatique des exceptions :   exécuter le bloc de code CAS EXCEPTION:
      //
      
      PROCEDURE ListeClients(nIDClient est un entier, sNomClient est une chaine, sEmailClient est une chaine)
      
      // Initialisation du mécanisme de remontée des erreurs
      InitialiseErreur()
      
      // Vérification de l'identification
      SI bEstConnectéWSBackOffice() = Faux ALORS
      	AjouteErreur(CST_MESSAGE_ERREUR_DROITS)
      	RENVOYER gtabClients
      FIN
      
      // Est-ce que les clients ont été initialisées ?
      SI gtabClients..Occurrence = 0 ALORS
      	// On initialise le contenu
      	ActualiserClients(nIDClient,sNomClient,sEmailClient)
      FIN
      
      // On renvoie la liste des clients
      RENVOYER gtabClients
      
      CAS EXCEPTION:
      	AjouteErreur("Exception détectée",ExceptionInfo())
      	RENVOYER gtabClients
     type : 458752
   -
     name : ActualiserClients
     internal_properties : CAAAAAgAAAAqhXbAQ85bubT0Czlm6MNqZZJeQibFMRa38jzNbVbSOpYNW7o5qNSpDkZCkDak4couHpZ7TlTRY36qMbY8Orz91WYwdLCsK17UjJ4FDTpxGr88rGoeQwgX5YrldbA95YA8m7wgBcZK9Qn0LwABGTRd8kBG46300PsWw7J8O/6Ov5tlHV8sT74rwa5wuJ35rAW3hJdWycI=
     procedure_id : 2219417895065926331
     type_code : 15
     code : |1-
      // Résumé : Actualise la liste des clients
      // Syntaxe :
      //[ <Résultat> = ] ActualiserClients (<nIDClient> est entier, <sNomClient> est chaîne, <sEmailClient> est chaîne)
      //
      // Paramètres :
      //	nIDClient (entier) : Identifiant du client (-1 pour ignorer)
      //	sNomClient (chaîne) : Nom du client à rechercher (chaine vide pour ignorer)
      //	sEmailClient (chaîne) : Email du client à rechercher (chaine vide pour ignorer)
      // Valeur de retour :
      // 	booléen : Vrai si les données sont mises à jour, Faux sinon
      //
      //// Traitement automatique des exceptions :   exécuter le bloc de code CAS EXCEPTION:
      //
      
      Procédure ActualiserClients( nIDClient est un entier, sNomClient est une chaine, sEmailClient est une chaine)
      
      // Initialisation du mécanisme de remontée des erreurs
      InitialiseErreur()
      
      // Vérification de l'identification
      SI bEstConnectéWSBackOffice() = Faux ALORS
      	AjouteErreur(CST_MESSAGE_ERREUR_DROITS)
      	RENVOYER Faux
      FIN
      
      // Initialisation de la requête : par défaut tous les critères sont ignorés
      REQ_RECHERCHE_CLIENT.pEmailClient 	= Null
      REQ_RECHERCHE_CLIENT.pNomClient		= Null
      REQ_RECHERCHE_CLIENT.pIDClient		= Null
      
      // Est-ce qu'il y a des paramètres à utiliser
      SI nIDClient >0 alors REQ_RECHERCHE_CLIENT.pIDClient = nIDClient
      SI sNomClient <>"" alors REQ_RECHERCHE_CLIENT.pNomClient = sNomClient
      SI sEmailClient <>"" alors REQ_RECHERCHE_CLIENT.pEmailClient = sEmailClient
      
      // Exécution de la requête
      SI HExécuteRequête(REQ_RECHERCHE_CLIENT) = Faux alors
      	AjouteErreur("Impossible d'exécuter la requête de recherche")
      	RENVOYER Faux
      FIN
      
      // On remplit le tableau
      FichierVersTableau(gtabClients,REQ_RECHERCHE_CLIENT)
      
      RENVOYER Vrai
      
      CAS EXCEPTION:	
      	AjouteErreur("Exception détectée",ExceptionInfo())
      	RENVOYER Faux
     type : 458752
   -
     name : bPossèdeCommandeEnTraitement
     internal_properties : CAAAAAgAAAAIDzCKeGUz1W/bMqHWfSIt1NQ0yKwnTaXFogTWBqrwHIv95Ije8fvfJKwR/3f+9jr8BG8dZGQ1PIrQJllvk3AjEp7AyIOavkAl8lLE6dUhXnVWK0w5v5lKuzUOGENKBnxG2qxHzxZVeamOPlJ0R4Ko6c1zFfaU0dE1XCbNV7eMHlGhpjj4HXUmhORcVGrW5Edy+6vef2K90cjm9oPWCHTz1dSNwHV3pLe6sKCxldMsvkj4IsC4/cA1JWJgCqAompcIYyRTIM/ZGslI+oMI/vFJ6gXZ+23x1MrhxPCa8EW+pY+dAtgjc8s/Oa6OiXJsLIWJjh2WD8zrt0szNspJVD1wDUg2VlNMZtOFjN6Ju11RedlN4sgX9+nH4eG1Tj9oyexeec+vYp6eGV9kdm5z/L5ggayR6UF1A826XJRiuiwHBA==
     procedure_id : 2238386957895298597
     type_code : 15
     code : |1-
      // Résumé : Indique si un client possède des commandes non traitées (ni reçue, ni annulée)
      // Syntaxe :
      //[ <Résultat> = ] bPossèdeCommandeEnTraitement (<nIDClient> est entier)
      //
      // Paramètres :
      //	nIDClient (entier) : Identifiant du client
      // Valeur de retour :
      // 	booléen : VRAI si au moins une commande en traitement
      //
      // Traitement automatique des exceptions :   exécuter le bloc de code CAS EXCEPTION:
      //
      
      PROCEDURE bPossèdeCommandeEnTraitement(nIDClient est un entier)
      
      // Initialisation du mécanisme de remontée des erreurs
      InitialiseErreur()
      
      // Vérification de l'identification
      SI bEstConnectéWSBackOffice() = Faux ALORS
      	AjouteErreur(CST_MESSAGE_ERREUR_DROITS)
      	RENVOYER Faux
      FIN
      
      // Pour toutes commandes de ce client
      POUR TOUT Commande AVEC IDClient = nIDClient
      	// SI la commande est en cours de traitement
      	SI commande.EtatCommande DANS (CDE_ATTENTEREGLEMENT, CDE_LIVRAISON, CDE_PREPARATION, CDE_PAYEE_CB_NONVERIFIE, CDE_PAYEE_CB_VERIFIE, CDE_PAYEE_CB_SUSPECTE) ALORS
      		// Il en existe au moins une, on renvoie vrai
      		RENVOYER Vrai
      	FIN
      	
      FIN
      // Aucune commande n'est en cours de traitement
      RENVOYER Faux
      
      CAS EXCEPTION:
      	AjouteErreur("Exception détectée",ExceptionInfo())
      	RENVOYER Faux
     type : 458752
   -
     name : SupprimerClient
     internal_properties : CAAAAAgAAAAeQeZWqR4YD9gNuw5ISpse4q5d4YfQh8vxSSLWonylqfGpzXAstIQfjtu5yNZJ9N/c91UXiZv53fcJCNn0zKQH2jXtD1BPimLproJX87E+1RbRiyhFUUvyPW0p6P5mipLcRRthNLb+/G4cHMU2fOcWaLmnLrlIMic+6HTcyeP/rQgO
     procedure_id : 2238387593732534152
     type_code : 15
     code : |1-
      // Résumé : Supprime un client s'il n'est pas relié à des commandes
      // Syntaxe :
      //[ <Résultat> = ] SupprimerClient (<nIDClient> est entier)
      //
      // Paramètres :
      //	nIDClient (entier) : Identifiant du client
      // Valeur de retour :
      // 	booléen : VRAI si la suppression a été effectuée, Faux sinon
      //
      // Traitement automatique des exceptions :   exécuter le bloc de code CAS EXCEPTION:
      //
      
      PROCEDURE SupprimerClient(LOCAL nIDClient est un entier)
      
      // Initialisation du mécanisme de remontée des erreurs
      InitialiseErreur()
      
      // Vérification de l'identification
      SI bEstConnectéWSBackOffice() = Faux ALORS
      	AjouteErreur(CST_MESSAGE_ERREUR_DROITS)
      	RENVOYER Faux
      FIN
      
      // Si ce client à des commande en cours, on ne peut pas le supprimer
      Si bPossèdeCommandeEnTraitement(nIDClient) ALORS
      	AjouteErreur(<§Rff000052339d6f9a¤fr-FR¤Ce client a des commandes en cours et ne peut être supprimé§>)
      	RENVOYER Faux
      FIN
      
      // Lecture du client
      HLitRecherchePremier(Client,IDClient,nIDClient) 
      SI Htrouve(Client) = Faux alors
      	AjouteErreur(<§Rff000152339f6f9a¤fr-FR¤Impossible de trouver le client dans la base§>)
      	RENVOYER Faux
      FIN
      
      // Suppression du client
      SI HSupprime(Client) = Faux alors
      	AjouteErreur(<§Rff00025233a16f9a¤fr-FR¤Impossible de supprimer le client§>)
      	RENVOYER Faux
      FIN
      
      // Par défaut, la suppression se déroule correctement
      RENVOYER Vrai
      
      CAS EXCEPTION:
      	AjouteErreur("Exception détectée",ExceptionInfo())
      	RENVOYER Faux
     type : 458752
  procedure_templates : []
  property_templates : []
 code_parameters :
  internal_properties : CAAAAAgAAACVGQgYbalU7DKC3oH4ItDrjtcTHXXV/EuH8q0IbhyQlw==
resources :
 string_res :
  identifier : 0x1eb15acf0550ddf6
  internal_properties : CAAAAAgAAACm76HWfKGWp33VjXInA4cRlqArlgTTA862QGt72W2ld5Y=
  strings :
   -
     text :
      fr-FR : Ce client a des commandes en cours et ne peut être supprimé
     index : 0
   -
     text :
      fr-FR : Impossible de trouver le client dans la base
     index : 1
   -
     text :
      fr-FR : Impossible de supprimer le client
     index : 2
custom_note :
 internal_properties : CAAAAAgAAABtB9HWVzrXO2+4NDRVK0vmzaNKrCKqH1DBX30lMmGZ
